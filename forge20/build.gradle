plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

forgeVersion = "1.20.1"

minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                betterdexrewards {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                betterdexrewards {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                betterdexrewards {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven { url 'https://jitpack.io' }

    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/server/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[revision]/[artifact].[ext]')
        }
    }

    maven {
        url "https://maven.envyware.co.uk/releases"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.1.44"

    implementation 'pixelmon:Pixelmon-1.20.1-9.2.0-server:9.2.0'

    implementation 'com.github.EnvyWare.ForgePlaceholderAPI:api:2.0.4'

    implementation group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'

    shadow group: 'com.envyful.api', name: 'commons', version: '5.0.1'
    shadow (group: 'com.envyful.api', name: 'forge20', version: '5.0.1') {
        transitive = false;
    }
    shadow (group: 'com.envyful.api', name: 'reforged20', version: '5.0.1') {
        transitive = false;
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    setArchiveClassifier('')
    setArchiveBaseName("${rootProject.name}-Forge")

    relocate('org.spongepowered.configurate', 'com.envyful.better.dex.rewards.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.better.dex.rewards.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.better.dex.rewards.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.better.dex.rewards.shade.gson')
    relocate('com.zaxxer', 'com.envyful.better.dex.rewards.shade.hikari')
    relocate('org.slf4j', 'com.envyful.better.dex.rewards.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.better.dex.rewards.shade.envy.api')
    relocate('org.bstats', 'com.envyful.better.dex.rewards.shade.bstats')
    relocate('com.google.common', 'com.envyful.better.dex.rewards.shade.guava')
    relocate('org.apache.logging', 'com.envyful.better.dex.rewards.shade.log4j')
    relocate('io.netty', 'com.envyful.better.dex.rewards.shade.netty')
    relocate('io.lettuce', 'com.envyful.better.dex.rewards.shade.lettuce')
    relocate('com.cronutils', 'com.envyful.better.dex.rewards.shade.cronutils')
    relocate('com.cronutils', 'com.envyful.better.dex.rewards.shade.cronutils')
    relocate('com.sun.el', 'com.envyful.better.dex.rewards.shade.sun.el')
    relocate('javax', 'com.envyful.better.dex.rewards.shade.javax')
    relocate('javax', 'com.envyful.better.dex.rewards.shade.javax')
    relocate('org.reactivestreams', 'com.envyful.better.dex.rewards.shade.reactivestreams')
    relocate('reactor', 'com.envyful.better.dex.rewards.shade.reactor')

    exclude "**/module-info.class"
    exclude "**/*.properties"
    exclude "**/*.xml"
    exclude "META-INF/versions/9/**"
    exclude "META-INF/native-image/**"
    exclude "META-INF/services/**"
}

jar.finalizedBy('shadowJar')
shadowJar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')